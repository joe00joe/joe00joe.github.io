**练习2.17**

求列表最后一个元素

```scheme
(define (last-pair lst)
  (cond ((null? lst) nil)
        ((null? (cdr lst)) (car lst))
        (else (last-pair (cdr lst)))
        )
  )
```

**练习2.18**

反转列表

```scheme
(define (reverse lst)
  (define (iter rest result)
    (if (null? rest)
        result
        (iter (cdr rest) (cons (car rest) result)))
    )
  (iter lst nil)
  )
```

**练习2.19**

```scheme
(define (no-more? coin-values)
  (if (null? coin-values)
      true
      false))

(define (except-first-denomination coin-values)
  (cdr coin-values))

(define (first-denomination coins)
  (car coins))
```

排列顺序不会影响。比如，`amount`为`3`，对于`1 2`和`2 1`两种`coins`，种数都是2。

**练习2.20**

筛选出变长序列`z`中与`x`相同奇偶性的元素

```scheme
(define (filte predicate items)
  (if (null? items)
      nil
      (if (predicate (car items))
          (cons (car items) (filte predicate (cdr items)))
          (filte predicate (cdr items))
      )
  ))

(define (same-parity x . z)
     (define (same-parity? item)
       (even? (+ x item))
     )
     (filte same-parity? z)
)
```

**练 习2.21**

```scheme
(define (square-list items)
  (map (lambda (x) (* x x)) items))

(define (square-list-t items)
  (if (null? items)
      nil
      (cons (* (car items) (car items)) (square-list-t (cdr items)))
     )
  )
```

**练习2.22**

为什么顺序相反？ 这个函数与练习2.18迭代法反转列表原理是一样的。

为什么cons中两个元素交换后也不行？交换后求出来值形如`(((1)2)3)`，这不是列表



**练习2.23**

for-each

```scheme
(define (for-each proc items)
  (if (null? items)
      true
      (and
        (proc (car items))
        (for-each proc (cdr items))
        )
      
  )
)

```

**练习2.24**

解释器打印结果`(1 (2 (3 4)))`

可表示为`(cons 1 (cons (cons  2 (cons (cons 3 (cons 4 nil)) nil)) nil))`

**练习2.25**

```scheme
(car (cdr (car (cdr (cdr t1)))))
(car (car t2))
(car (cdr (car (cdr(car(cdr(car (cdr(car (cdr(car (cdr(car (cdr t3))))))))))))))
```

**练习2.26**

```scheme
(1 2 3 4 5 6)
((1 2 3) 4 5 6)
((1 2 3) (4 5 6))
```

**练习2.27**

反转树

```scheme
(define (deep-reverse tree)
  (cond ((null? tree) nil)
        ((not (pair? tree)) tree )
        (else (reverse (map  deep-reverse tree)))
   )
)
```



**练习2.28**

求叶子节点序列

```scheme
(define (fringe tree)
  (cond ((null?  tree) nil)
        ( (not ( pair? tree)) (list tree) )
        (else (append (fringe (car tree)) (fringe (cdr tree)) ))
  )
)
```

**练习2.29**

a）二叉活动体构造器和选择器

```scheme
(define (make-mobile left right)
  (list left right))

(define (make-branch length structure)
  (list length structure))

(define (left-branch t )
  (car t))

(define (right-branch t)
  (car (cdr t)))

(define (branch-length b)
  (car b))

(define (branch-structure b)
  (car (cdr b)))
```



b)求二叉活动体的重量

```scheme
(define (total-weigth mobile)
  (if (null? mobile)
      0
      (if (not (pair? mobile))
          mobile
          (+ (total-weigth ( branch-structure (right-branch mobile)))
             (total-weigth ( branch-structure (left-branch mobile)))
             )
       )
  )
```

c)判断是否平衡

```scheme
(define (balance-mobile? mobile)
  (if (null? mobile)
      true
      (if (not (pair? mobile))
          true
          (let ((left (left-branch mobile))
                (rigth (right-branch mobile)))
            (and  (= (* (branch-length left) (total-weigth (branch-structure left)))
                     (* (branch-length rigth) (total-weigth (branch-structure rigth))))
                  (balance-mobile? ( branch-structure (right-branch mobile)))
                  (balance-mobile? ( branch-structure (left-branch mobile)))
            )
             )
           )
      )
         
  )
```

d)只需改变选择器

```scheme
(define (left-branch t )
  (car t))

(define (right-branch t)
  (cdr t))

(define (branch-length b)
  (car b))

(define (branch-structure b)
  (cdr b))
```

**练习2.30**

```scheme
(define (square-tree tree)
  (cond ((null? tree) nil)
        ((not (pair? tree)) (* tree tree))
        (else (cons (square-tree (car tree))
                    (square-tree (cdr tree)))
              )
   )
)

(define (square-tree-t tree)
  (map (lambda (sub)
         (if (pair? sub)
             (square-tree-t sub)
             (* sub sub)
             )
         )
       tree)
)
```

**练习2.31**

```scheme
(define (tree-map fn tree)
  (cond ((null? tree) nil)
        ((not (pair? tree)) (fn tree))
        (else (cons (tree-map fn (car tree))
                    (tree-map fn (cdr tree)))
              )
   )
)
```

**练习2.32**

求全集

```scheme
(define (subsets s)
  (if (null? s)
      (list nil)
      (let ((rest (subsets (cdr s))))
        (append rest (map (lambda (x) (cons (car s) x)) rest))
      )
  )
)
```

**练习2.33**

```scheme
(define (map-t p sequence)
  (accumulate (lambda (x y) (cons (p x) y)) nil sequence)
  )
(define (append-t seq1 seq2)
  (accumulate cons seq2 seq1))
(define (length-t sequence)
  (accumulate (lambda (x y) (+ 1 y)) 0 sequence))
```

**练习2.34**

```scheme
(define (horner-eval x coefficient-sequence)
  (accumulate (lambda (this-coeff higher-terms) (+ this-coeff (* x higher-terms)))
              0
              coefficient-sequence))
```

**练习2.35**

```scheme
(define (count-leaves t)
  (accumulate (lambda (x y) (+ x y))
              0
              (map (lambda (sub-tree) (if (not (pair? sub-tree))
                                          1
                                          (count-leaves sub-tree))
                    )
                    t
              )
  )

)
```

**练习2.36**

```scheme

(define (accumulate-n op init seqs)
  (if (null? (car seqs))
      nil
      (cons (accumulate op init (map car seqs))
            (accumulate-n op init (map cdr seqs))))
  )
```

**练习2.37**

```scheme
(define (dot-product v w)
  (accumulate + 0 (map * v w)))
(define (matrix-*-vector m v)
  (map (lambda (row) (dot-product row v)) m)
  )
(define (transpose mat)
  (accumulate-n  (lambda (x y) (cons x y)) `() mat)
  )

(define (matrix-*-matrix m n)
   (let ((cols (transpose n)))
     (map (lambda (v) (matrix-*-vector cols v) ) m)))
  
```

**练习2.38**

`3/2`
`1/6`
`(1 (2 (3 ())))`
`(((() 1) 2) 3)`

当`a op b`等于`b op a`时

**练习2.39**

```scheme
(define (reverse-t1 sequence)
  (fold-right (lambda (x y) (append y (list x))) nil sequence))

(define (reverse-t2 sequence)
  (fold-left (lambda (x y) (cons y x)) nil sequence))
```

**练习2.40**

```scheme
(define (unique-pairs n)
    (flatmap (lambda (i)
                 (map (lambda (j) (list i j))
                      (enumerate-interval 1 (- i 1))))
             (enumerate-interval 1 n)))
             
(define (prime-sum-pairs n)
    (map make-pair-sum
         (filter prime-sum? (unique-pairs n))))
```

**练习2.41**

```scheme
(define (sum-list lst)
  (accumulate + 0 lst))

(define (s-sum-triplets n s)
  (filter (lambda (t) (= s (sum-list t)) )
          (flatmap  (lambda (i)
                        (map (lambda (k) (append i (list k)))
                             (enumerate-interval 1 ( - (list-ref i 1) 1)))
                    )
                    (unique-pairs n)
          )))
```

**练习2.42**

使用二维列表作为棋盘表示，代码有些丑陋。若使用一维列表(第k个元素表示第k列皇后的行坐标)实现会更简洁。列表中。

```scheme
(define (matrix-ref m i j)
    (list-ref (list-ref m (- i 1)) (- j 1)))
(define (queens board-size)
    (define empty-board  (map (lambda (x) nil)  (enumerate-interval 1 board-size)))
    (define (adjoin-position new-row  k rest-of-queens)
      (map  (lambda (p) (if (= new-row (car p) )
                            (append (cdr p) (list 1))
                            (append (cdr p) (list 0))))
             (map (lambda (i) (cons i (list-ref rest-of-queens (- i 1))) )
                  (enumerate-interval 1 board-size ))
            )
    )
    (define (vaild?  position)
      (let ((row (car position))
            (col (cdr position)))
         (and (> row 0) (< row (+ board-size  1)) (> col 0) (< col (+ board-size  1)) )
        )
    )
    
    (define (safe? k positions)
      (define (line-safe? next  p)
         (cond ((not (vaild? p)) true)
            (( = 1 (matrix-ref positions (car p) (cdr p))) false)
            (else (line-safe?  next (next p))))
      )
      (define (kth-row-queen-p)
         (define (iter row)
           (cond ((> row board-size) nil)
                 ((= (matrix-ref positions row k) 1) row)
                 (else (iter (+ row 1)))
               ))
         (iter 1)
        )
      (let ((start (cons (kth-row-queen-p) k)))
        (and (line-safe? 
              (lambda (p) (cons (car p) (- (cdr p) 1))) 
              (cons (car start) (- (cdr start) 1)))
             (line-safe? 
              (lambda (p) (cons (- (car p) 1) (- (cdr p) 1))) 
              (cons ( - (car start) 1) (- (cdr start) 1)))
             (line-safe? 
              (lambda (p) (cons (+ (car p) 1) (- (cdr p) 1))) 
              (cons ( + (car start) 1) (- (cdr start) 1)))
             )
        )
     )
   (define (queen-cols k)
     (if (= k 0)
         (list empty-board)
         (filter
          (lambda (positions) (safe? k positions))
          (flatmap
           (lambda (rest-of-queens)
             (map (lambda (new-row)
                    (adjoin-position new-row k rest-of-queens))
                  (enumerate-interval 1 board-size)))
           (queen-cols (- k 1))))))
  (queen-cols board-size)
) 
```

**练习2.43**

略

**练习2.44**

```scheme
(define (up-split painter n)
  (if (= n 0)
      painter
      (let ((smaller (up-split painter (- n 1))))
        (below  painter (beside smaller smaller)))))
```



**练习2.45**

```scheme
(define (split combiner1 combiner2 )
    (define (split-helper painter n)
      (if (= n 0)
          painter
          (let ((smaller (split-helper painter (- n 1))))
            (combiner1  painter (combiner2 smaller smaller)))
          ))

     split-helper
)
```



**练习2.46**

二维向量

```scheme
(define (make-vector x y)
  (cons x y))
(define (xcor-vect v)
  (car v))
(define (ycor-vect v)
  (cdr v))

(define (add-vect v1 v2)
  (cons (+ (xcor-vect v1) (xcor-vect v2)) (+ (ycor-vect v1) (ycor-vect v2)))
  )

(define (sub-vect v1 v2)
  (cons (- (xcor-vect v1) (xcor-vect v2)) (- (ycor-vect v1) (ycor-vect v2)))
  )

(define (scale-vect v s)
  (cons (* s (xcor-vect v)) (* s (ycor-vect v))))

```

**练习2.47**

框架实现

```scheme
;方法一
(define (make-frame origin edge1 edge2)
  (list origin edge1 edge2))

(define (origin-frame f)
  (list-ref f 0))

(define (edge1-frame f)
  (list-ref f 1))


(define (edge2-frame f)
  (list-ref f 2))
;方法二
(define (make-frame origin edge1 edge2)
  (cons origin (cons edge1 edge2)))

(define (origin-frame f)
  (car f))

(define (edge1-frame f)
  (car (cdr f)))

(define (edge2-frame f)
  (cdr (cdr f)))
```

**练习2.48**

```scheme
(define (make-segment start end)
  (cons start end))
(define (start-segment s)
  (car s))
(define (end-segment s)
  (cdr s))
```

**练习2.49**

首先实现`draw-line` ,此实面依赖于`racket/gui`库

```scheme
;线段集合
(define lines '())
;向线段集合中添加线段
(define (draw-line start end)
  (let ((x1 (xcor-vect start))
        (y1 (ycor-vect start))
        (x2 (xcor-vect end))
        (y2 (ycor-vect end)))
    (set! lines (cons (list x1 y1 x2 y2) lines))
  )
  )
 
;绘制
(define (plt-show)
  (define frame (new frame% [label "Draw"]
                     [width 400]
                     [height 400]))
  (define canvas (new canvas% [parent frame]
                      [paint-callback
                       (lambda (canvas dc)
                         (for-each (lambda (line)
                                     (send dc draw-line
                                           (list-ref line 0) (list-ref line 1) (list-ref line 2) (list-ref line 3)))
                                   lines))]))
  (send frame show #t))

```

a)画出给定框架边界的画家

```scheme
(define lower (make-segment (make-vector 0 0) (make-vector 1 0)))
(define upper (make-segment (make-vector 0 1) (make-vector 1 1)))
(define left (make-segment (make-vector 0 0) (make-vector 1 0)))
(define righ (make-segment (make-vector 1 0) (make-vector 1 1)))
(define sgL (list  lower upper left righ))

(define point-a (segments->painter sgL))
```

b)画出框架对角线的画家

```scheme
(define diag1 (make-segment (make-vector 0 0) (make-vector 1 1)))
(define diag2 (make-segment (make-vector 1 0) (make-vector 0 1)))

(define sgL (list  diag1 diag2 ))
(define point-b (segments->painter sgL))
```

c)画出框架内接菱形的画家

```scheme
(define lower (make-segment (make-vector 0 0) (make-vector 1 0)))
(define upper (make-segment (make-vector 0 1) (make-vector 1 1)))
(define left (make-segment (make-vector 0 0) (make-vector 0 1)))
(define right (make-segment (make-vector 1 0) (make-vector 1 1)))

(define mid-lower (midpoint-segment lower))
(define mid-upper (midpoint-segment upper))
(define mid-left (midpoint-segment left))
(define mid-right (midpoint-segment right))

(define low-left (make-segment mid-lower mid-left))
(define left-upper (make-segment mid-left mid-upper))
(define upper-right (make-segment mid-upper mid-right))
(define right-lower (make-segment mid-right mid-lower))

(define sgL (list  low-left left-upper upper-right right-lower))

(define point-c (segments->painter sgL))
```

d)wave

```scheme
(define make-vect make-vector)

(define wave (segments->painter (list
                         (make-segment (make-vect 0.4 1.0)      ; 头部左上
                                       (make-vect 0.35 0.85))
                         (make-segment (make-vect 0.35 0.85)    ; 头部左下
                                       (make-vect 0.4 0.64))
                         (make-segment (make-vect 0.4 0.65)     ; 左肩
                                       (make-vect 0.25 0.65))
                         (make-segment (make-vect 0.25 0.65)    ; 左手臂上部
                                       (make-vect 0.15 0.6))
                         (make-segment (make-vect 0.15 0.6)     ; 左手上部
                                       (make-vect 0.0 0.85))

                         (make-segment (make-vect 0.0 0.65)     ; 左手下部
                                       (make-vect 0.15 0.35))
                         (make-segment (make-vect 0.15 0.35)    ; 左手臂下部
                                       (make-vect 0.25 0.6))

                         (make-segment (make-vect 0.25 0.6)     ; 左边身体
                                       (make-vect 0.35 0.5))
                         (make-segment (make-vect 0.35 0.5)     ; 左腿外侧
                                       (make-vect 0.25 0.0))
                         (make-segment (make-vect 0.6 1.0)      ; 头部右上
                                       (make-vect 0.65 0.85))
                         (make-segment (make-vect 0.65 0.85)    ; 头部右下
                                       (make-vect 0.6 0.65))
                         (make-segment (make-vect 0.6 0.65)     ; 右肩
                                       (make-vect 0.75 0.65))
                         (make-segment (make-vect 0.75 0.65)    ; 右手上部
                                       (make-vect 1.0 0.3))

                         (make-segment (make-vect 1.0 0.15)     ; 右手下部
                                       (make-vect 0.6 0.5))
                         (make-segment (make-vect 0.6 0.5)      ; 右腿外侧
                                       (make-vect 0.75 0.0))

                         (make-segment (make-vect 0.4 0.0)      ; 左腿内侧
                                       (make-vect 0.5 0.3))
                         (make-segment (make-vect 0.6 0.0)      ; 右腿内侧
                                       (make-vect 0.5 0.3))))
  )
```

**练习2.50**

```scheme
(define (flip-horiz pointer)
  (transform-painter pointer
                    (make-vector 1.0 0.0)
                    (make-vector 0.0 0.0)
                    (make-vector 1.0 1.0)))


(define (rotate-180 pointer)
  (transform-painter pointer
                    (make-vector 1.0 1.0)
                    (make-vector 0.0 1.0)
                    (make-vector 1.0 0.0)))

(define (rotate-270 pointer)
  (transform-painter pointer
                    (make-vector 0.0 1.0)
                    (make-vector 0.0 0.0)
                    (make-vector 1.0 1.0)))
```

**练习2.51**

```scheme
(define (below painter1 painter2)
  (let ((split-point (make-vector 0.0 0.5)))
    (let ((paint-buttom
           (transform-painter painter1
                              (make-vector 0.0 0.0)
                              (make-vector 1.0 0.0)
                              split-point))
          (paint-top
           (transform-painter painter2
                              split-point
                              (make-vector 1.0 0.5)
                              (make-vector 0.0 1.0)))
          )
          (lambda (frame)
            (paint-buttom frame)
            (paint-top frame)))))
 ;beside加旋转
 (define (below-t painter1 painter2)
  (rotate-90 (beside (rotate-270 painter1) (rotate-270 painter2)))
  )

```

**练习2.52**

a)略

b)

```scheme
(define (corner-split painter n)
    (if (= n 0)
        painter
        (let ((up (up-split painter (- n 1)))
              (right (right-split painter (- n 1)))
              (corner (corner-split painter (- n 1))))
            (beside (below painter up)
                    (below right corner)))))
```

c)

```scheme
define (square-limit painter n)
    (let ((combine4 (square-of-four identity flip-horiz)
                                    flip-vect rotate180))
        (combine4 (corner-split painter n))))
```



